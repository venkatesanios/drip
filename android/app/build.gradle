plugins {
    id "com.android.application"
    id 'com.google.gms.google-services' // Firebase
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

// Load key.properties if present
def keystorePropertiesFile = rootProject.file("android/key.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    keystoreProperties.each { key, value ->
        project.ext.set(key, value)
    }
}

// Defaults from local.properties
def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localProperties.load(localPropertiesFile.newReader("UTF-8"))
}

def flutterVersionCode = localProperties.getProperty("flutter.versionCode") ?: "1"
def flutterVersionName = localProperties.getProperty("flutter.versionName") ?: "1.0"

// Extract key values from loaded props
def KEYSTORE_PASSWORD = project.hasProperty("storePassword") ? project.storePassword : ""
def KEY_ALIAS = project.hasProperty("keyAlias") ? project.keyAlias : ""
def KEY_PASSWORD = project.hasProperty("keyPassword") ? project.keyPassword : ""

android {
    namespace = "com.niagaraautomations.oroDripirrigation"
    compileSdk = 35
    ndkVersion = flutter.ndkVersion

    flavorDimensions += "flavor-type"

    productFlavors {
        oroDevelopment {
            dimension "flavor-type"
            applicationId "com.niagaraautomations.oroDevelopment"
            resValue "string", "app_name", "ORO-Dev"
        }
        oroProduction {
            dimension "flavor-type"
            applicationId "com.niagaraautomations.oroDripirrigation"
            resValue "string", "app_name", "ORO"
        }
        smartComm {
            dimension "flavor-type"
            applicationId "com.niagaraautomations.smartComm"
            resValue "string", "app_name", "SMART COMM"
        }
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                storeFile file("../build/keystore/release-key.jks")
                storePassword KEYSTORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
            } else {
                // Local fallback
                storeFile file("D:/drip/android/app/release-key.jks")
                storePassword "Oro@321"
                keyAlias "upload"
                keyPassword "Oro@321"
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    defaultConfig {
        applicationId = "com.niagaraautomations.oroDripirrigation"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

flutter {
    source = "../.."
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
}
